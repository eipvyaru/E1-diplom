#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ВКМ_ДлительноеМассовоеСозданиеАктовРТУ(Параметры) Экспорт
	МДоговоры = Новый Массив;
	МРТУ = Новый Массив;
	Рез = Новый Структура;
	Рез.Вставить("МДоговоры", МДоговоры);
	Рез.Вставить("МРТУ", МРТУ);
	
	//Выбрать договоры

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДоговораОбслуживания <= &Дт
	|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДоговораОбслуживания >= &Дт
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И НЕ ДоговорыКонтрагентов.Ссылка В
	|		(ВЫБРАТЬ
	|			РеализацияТоваровУслуг.Договор
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ГДЕ
	|			РеализацияТоваровУслуг.Пометкаудаления = ЛОЖЬ
	|			И РеализацияТоваровУслуг.Организация = &Организация
	|			И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, месяц) = НАЧАЛОПЕРИОДА(&Дт, месяц))";

	Запрос.УстановитьПараметр("Дт", НачалоМесяца(Параметры.Месяц));
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);

	РезультатЗапроса = Запрос.Выполнить();

	ВДЗ = РезультатЗапроса.Выгрузить();
	ВсегоДоговоров = ВДЗ.Количество();
	Если ВсегоДоговоров = 0 Тогда
		Возврат Рез;
	КонецЕсли;
	МДоговоры =ВДЗ.ВыгрузитьКолонку("Ссылка"); 

	//Для каждого договора создать РТУ
	ОбработаноДоговоров = 0;
	Для Каждого Дог Из ВДЗ Цикл
//создать документ РТУ
		ДокОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Дата", КонецМесяца(Параметры.Месяц));
		ДанныеЗаполнения.Вставить("Организация", Параметры.Организация);
		ДанныеЗаполнения.Вставить("Договор", Дог.Ссылка);
	//@skip-check bsl-legacy-check-string-literal
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Дог.Ссылка, "Владелец");
		ДанныеЗаполнения.Вставить("Контрагент", Контрагент);
		ДанныеЗаполнения.Вставить("Комментарий", "Создано обработкой ВКМ_ДлительноеМассовоеСозданиеАктовРТУ");
		ДокОбъект.Заполнить(ДанныеЗаполнения);
//автозаполнить работами и обслуживанием
		ДокОбъект.ВКМ_ВыполнитьАвтозаполнение();
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
		МРТУ.Добавить(ДокОбъект.Ссылка);
		ОбработаноДоговоров = ОбработаноДоговоров + 1;
		ДлительныеОперации.СообщитьПрогресс(ОбработаноДоговоров / ВсегоДоговоров * 100);
	КонецЦикла;

	Рез.Очистить();
	Рез.Вставить("МДоговоры", МДоговоры);
	Рез.Вставить("МРТУ", МРТУ);
	Возврат Рез;
КонецФункции
#КонецОбласти
#КонецЕсли