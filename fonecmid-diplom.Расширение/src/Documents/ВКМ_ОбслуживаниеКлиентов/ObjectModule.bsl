#Если НаСервере Тогда
#Область Обработчикисобытий


Процедура ОбработкаПроведения(Отказ, Режим)
		//% сотрудника
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_ПроцентОтРабот
			|ИЗ
			|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, ВКМ_Сотрудник = &Сотрудник) КАК
			|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
		
		Запрос.УстановитьПараметр("Сотрудник", ВКМ_Специалист);
		Запрос.УстановитьПараметр("Дата", Дата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВДЗ = РезультатЗапроса.Выбрать();
		
		Если ВДЗ.Следующий() Тогда
			Процент = ВДЗ.ВКМ_ПроцентОтРабот;
		Иначе
			Сообщ = СтрШаблон("Для специалиста %1 на дату %2 не установлен процент от оплаты клиента. Установите."
								,Строка(ВКМ_Специалист),Формат(Дата,"ДФ=dd.MM.yyyy;"));
			ОбщегоНазначения.СообщитьПользователю(Сообщ);
			Отказ= Истина;
			Возврат;
		КонецЕсли;


	РеквДогов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВКМ_Договор,
		"ВКМ_СтоимостьЧасаРаботыСпециалиста,ВКМ_ДатаНачалаДоговораОбслуживания,ВКМ_ДатаОкончанияДоговораОбслуживания");
	Если НЕ(РеквДогов.ВКМ_ДатаНачалаДоговораОбслуживания <= Дата И РеквДогов.ВКМ_ДатаОкончанияДоговораОбслуживания
		>= НачалоДня(Дата)) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			"Дата документа за пределами периода действия указанного договора. Проведение отменено.");
		Возврат;
	КонецЕсли;
	
	СтавкаЧасаКлиента  = РеквДогов.ВКМ_СтоимостьЧасаРаботыСпециалиста;
		// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
		
		
		// регистр ВКМ_ВыполненныеСотрудникомРаботы
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	
	Для Каждого ТекСтрокаВКМ_ВыполненныеРаботы Из ВКМ_ВыполненныеРаботы Цикл
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.ВКМ_Организация = ВКМ_Организация;
		Движение.ВКМ_Клиент = ВКМ_Клиент;
		Движение.ВКМ_Договор = ВКМ_Договор;
		Движение.ВКМ_ЧасыКОплатеКлиенту = Час(ТекСтрокаВКМ_ВыполненныеРаботы.ВКМ_ЧасыКОплатеКлиенту) + (Минута(
			ТекСтрокаВКМ_ВыполненныеРаботы.ВКМ_ЧасыКОплатеКлиенту) / 60);
		Движение.ВКМ_СуммаКОплате = Движение.ВКМ_ЧасыКОплатеКлиенту * СтавкаЧасаКлиента ;


		Движение2 = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение2.Период = Дата;
		Движение2.ВКМ_Сотрудник = ВКМ_Специалист;
		Движение2.ВКМ_ЧасовКОплате = Движение.ВКМ_ЧасыКОплатеКлиенту;
		Движение2.ВКМ_СуммаКОплате = Движение.ВКМ_СуммаКОплате*Процент/100;
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Проверить изменения даты, времени или специалиста
	УведомитьТБота = "";
	Если Не ЭтоНовый() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
					   |	Документ1.Дата,
					   |	Документ1.ВКМ_Специалист,
					   |	Документ1.ВКМ_ДатаПроведенияРабот,
					   |	Документ1.ВКМ_ВремяНачалаРаботПлан,
					   |	Документ1.ВКМ_ВремяОкончанияРаботПлан
					   |ИЗ
					   |	Документ.ВКМ_ОбслуживаниеКлиентов КАК Документ1
					   |ГДЕ
					   |	Документ1.Ссылка = &Ссылка";

		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();

		Если Выборка.Следующий() Тогда
			Если ВКМ_Специалист <> Выборка.ВКМ_Специалист Тогда
				ИзменСпец = СтрШаблон("Изменен специалист %1 на %2.", Строка(Выборка.ВКМ_Специалист), Строка(
					ВКМ_Специалист));
			Иначе
				ИзменСпец = "";
			КонецЕсли;
			Если ВКМ_ДатаПроведенияРабот <> Выборка.ВКМ_ДатаПроведенияРабот Тогда
				ИзменДт = СтрШаблон("Изменена дата работ %1 на %2.", формат(Выборка.ВКМ_ДатаПроведенияРабот,
					"ДФ=dd.MM.yy;"), формат(ВКМ_ДатаПроведенияРабот, "ДФ=dd.MM.yy;"));
			Иначе
				ИзменДт = "";
			КонецЕсли;

			Если ВКМ_ВремяНачалаРаботПлан <> Выборка.ВКМ_ВремяНачалаРаботПлан Тогда
				ИзменВр = СтрШаблон("Изменено время работ  %1 на %2.", формат(Выборка.ВКМ_ВремяНачалаРаботПлан,
					"ДФ=ЧЧ:мм;"), формат(ВКМ_ВремяНачалаРаботПлан, "ДФ=ЧЧ:мм;"));
			Иначе
				ИзменВр = "";
			КонецЕсли;

			Если ЗначениеЗаполнено(ИзменСпец) Или ЗначениеЗаполнено(ИзменДт) Или ЗначениеЗаполнено(ИзменВр) Тогда
				УведомитьТБота = СтрШаблон("Изменена работа у клиента %1. %2 %3 %4", ВКМ_Клиент, ИзменСпец, ИзменДт,
					ИзменВр);
			КонецЕсли;

		КонецЕсли;
	Иначе
		УведомитьТБота = СтрШаблон("Навая работа у клиента %1. Специалис %2, Дата %3, Время %4", ВКМ_Клиент,
			ВКМ_Специалист, ВКМ_ДатаПроведенияРабот, ВКМ_ВремяНачалаРаботПлан);
	КонецЕсли;

	Если ЗначениеЗаполнено(УведомитьТБота) Тогда
		НовУвед = Справочники.ВКМ_УведомленияТелеграмБотуДляОтправки.СоздатьЭлемент();
		НовУвед.ВКМ_ТекстСообщения = УведомитьТБота;
		НовУвед.Записать();
		ВКМ_ТелеграмВызовСервера.ОтправитьВгруппуТелеграм(УведомитьТБота);
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

#КонецЕсли